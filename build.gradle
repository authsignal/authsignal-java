plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.19.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'junit:junit:4.13.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.authsignal'
            artifactId = 'authsignal-java'
            version = project.version

            from components.java

            pom {
                name = 'authsignal-java'
                description = 'The Authsignal Server SDK for Java.'
                url = 'https://github.com/authsignal/authsignal-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/authsignal/authsignal-java/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'Authsignal'
                        name = 'Authsignal'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/authsignal/authsignal-java.git'
                    developerConnection = 'scm:git:ssh://github.com/authsignal/authsignal-java.git'
                    url = 'https://github.com/authsignal/authsignal-java/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        java {
            groupId = 'com.authsignal'
            artifactId = 'authsignal-java'
            description = 'The Authsignal Server SDK for Java.'
            version = project.version
        }
    }
    signing {
        active = 'ALWAYS'
        publicKey = System.getenv('SIGNING_PUBLIC_KEY')
        secretKey = System.getenv('SIGNING_SECRET_KEY')
        passphrase = System.getenv('SIGNING_PASSPHRASE')
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = System.getenv('MAVENCENTRAL_USERNAME')
                    password = System.getenv('MAVENCENTRAL_PASSWORD')
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

test {
    testLogging.showStandardStreams = true
}